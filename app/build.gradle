apply plugin: 'com.android.application'

// Reference: https://github.com/nickbutcher/plaid/blob/master/app/build.gradle
def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "name.vampidroid"
        minSdkVersion 9
        targetSdkVersion 25
        versionCode gitCommitCount
        versionName gitTag

        resValue "string", "version_name", gitTag
    }

    buildTypes {
//        Reference: http://stackoverflow.com/questions/18491649/how-to-change-the-android-app-package-name-when-assembling-with-gradle
//        Reference: https://github.com/futurice/android-best-practices
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }

        beta {
            applicationIdSuffix '.beta'
            versionNameSuffix '-BETA'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.android.support:recyclerview-v7:25.2.0'
    compile 'com.android.support:cardview-v7:25.2.0'
    compile 'com.android.support:percent:25.2.0'
    compile 'com.android.support:preference-v7:25.2.0'
    compile 'com.android.support:palette-v7:25.2.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'


    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    compile 'com.jakewharton.rxbinding2:rxbinding-design:2.0.0'

    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC1'
}
